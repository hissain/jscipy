plugins {
    id 'java-library'
    id 'maven-publish'
    id 'application'
}


group = 'com.hissain.jscipy'
version = '3.1.4'

application {
    mainClassName = 'com.hissain.jscipy.Main'
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.apache.commons:commons-math3:3.6.1'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.9.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.9.1'
}

java {
    // Gradle Toolchain: Requires Adoptium JDK 8
    // This forces auto-download if only JRE or non-Adoptium Java is found
    toolchain {
        languageVersion = JavaLanguageVersion.of(8)
        vendor = JvmVendorSpec.ADOPTIUM
    }
    
    withSourcesJar()
    withJavadocJar()
}

sourceSets {
    main {
        java {
            srcDirs = ['src/main/java']
        }
        resources {
            srcDirs = ['src/main/resources']
        }
    }
    test {
        java {
            srcDirs = ['src/test/java']
        }
        resources {
            srcDirs = ['src/test/resources']
        }
    }
}

test {
    useJUnitPlatform()
    testLogging {
        showStandardStreams = true
    }
}

tasks.withType(Javadoc) {
    destinationDir = file("$rootDir/docs/javadoc")
    options.addStringOption('Xdoclint:none', '-quiet')
    options.addStringOption('Xmaxwarns', '0') // Suppress all warnings
    options.addStringOption('Xmaxerrs', '0') // Suppress all errors
    exclude '**/Main.java'
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId = 'com.hissain.jscipy'
            artifactId = 'jscipy'
            version = '3.1.4'

            from components.java
        }
    }
}
